compileJava { options.fork = true }

jar {
    manifest {
        attributes 'Implementation-Title': 'Spring boot starter for Logger',
                'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts { archives javadocJar, sourcesJar }

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask(uploadArchives) }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: rootProject.hasProperty('ossrhUsername') ? rootProject.ossrhUsername : '',
                        password: rootProject.hasProperty('ossrhPassword') ? rootProject.ossrhPassword : '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: rootProject.hasProperty('ossrhUsername') ? rootProject.ossrhUsername : '',
                        password: rootProject.hasProperty('ossrhPassword') ? rootProject.ossrhPassword : '')
            }

            pom.project {
                name 'Spring Boot Logger Starter'
                packaging 'jar'
                description 'Spring boot starter for Logger'
                url 'https://github.com/rozidan/springboot-boot-starter-logger.git'

                scm {
                    connection 'scm:git:https://github.com/rozidan/springboot-boot-starter-logger.git'
                    developerConnection 'scm:git:https://github.com/rozidan/springboot-boot-starter-logger.git'
                    url 'https://github.com/rozidan/springboot-boot-starter-logger.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'rozidan'
                        name 'Idan Rozenfeld'
                        email 'rozidan@gmail.com'
                    }
                }
            }
        }
    }
}

release {
    tagTemplate = '${version}'
    git {
        requireBranch = 'master'
        pushToRemote = 'origin'
    }
}
afterReleaseBuild.dependsOn uploadArchives